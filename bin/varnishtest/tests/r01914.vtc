varnishtest "Set the storage backend for the request body"

server s1 {
	rxreq
	expect req.bodylen == 100
	txresp
	rxreq
	expect req.bodylen == 100
	txresp
} -start

varnish v1 -arg "-s malloc,1MB" -arg "-s malloc,1MB" -vcl+backend {
	import std;
	sub vcl_recv {
		if (req.url == "/1") {
			set req.storage = storage.s1;
		}
		std.cache_req_body(1KB);
	}
	sub vcl_backend_fetch {
		return (fetch);
	}
	sub vcl_backend_response {
		set beresp.storage = storage.s0;
	}
} -start

client c1 {
	txreq -url /0 -bodylen 100
	rxresp
} -run

varnish v1 -expect SMA.s0.c_bytes > 0
varnish v1 -expect SMA.s1.c_bytes == 0
varnish v1 -expect SMA.Transient.c_bytes > 0

client c1 {
	txreq -url /1 -bodylen 100
	rxresp
} -run

varnish v1 -expect SMA.s0.c_bytes > 0
varnish v1 -expect SMA.s1.c_bytes > 0
varnish v1 -expect SMA.Transient.c_bytes > 0
