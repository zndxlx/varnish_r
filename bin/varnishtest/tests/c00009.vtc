varnishtest "Test restarts"

server s1 {
	rxreq
	expect req.url == "/foo"
	txresp -status 404
} -start

server s2 {
	rxreq
	expect req.url == "/foo"
	txresp -body "foobar"
} -start

varnish v1 -vcl+backend {
	sub vcl_recv {
		set req.http.restarts = req.restarts;
	}
	sub vcl_backend_fetch {
		if (bereq.http.restarts == "0") {
			set bereq.backend = s1;
		} else {
			set bereq.backend = s2;
		}
	}

	sub vcl_backend_response {
		if (beresp.status != 200) {
			set beresp.uncacheable = true;
			set beresp.ttl = 0s;
			return (deliver);
		}
	}
	sub vcl_deliver {
		if (resp.status != 200) {
			return (restart);
		}
	}
} -start

client c1 {
	txreq -url "/foo"
	rxresp
	expect resp.status == 200
	expect resp.bodylen == 6
}

client c1 -run
