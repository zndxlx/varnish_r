varnishtest "Test parking second request on backend delay, then pass"

barrier b1 cond 2

server s1 {
	rxreq
	expect req.url == "/foo"
	barrier b1 sync
	send "HTTP/1.1 200 OK\r\nContent-Length: 12\r\n\r\n"
	send "line1\n"
	send "line2\n"

	rxreq
	expect req.url == "/foo"
	txresp -body "foobar"
} -start

varnish v1 -vcl+backend {
	sub vcl_backend_response {
		set beresp.do_stream = false;
		set beresp.uncacheable = true;
	}
} -start

client c1 {
	txreq -url "/foo"
	rxresp
	expect resp.status == 200
	expect resp.bodylen == 12
	expect resp.http.x-varnish == "1001"
} -start

barrier b1 sync
delay .2

client c2 {
	txreq -url "/foo"
	rxresp
	expect resp.status == 200
	expect resp.bodylen == 6
	expect resp.http.x-varnish == "1004"
} -run

client c1 -wait

varnish v1 -expect cache_hitpass == 0
varnish v1 -expect cache_hitmiss == 1
varnish v1 -expect cache_miss == 2
